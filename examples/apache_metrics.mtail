# Copyright 2015 Ben Kochie <superq@gmail.com>. All Rights Reserved.
# This file is available under the Apache license.

# Parser for a metrics-friendly apache log format
# LogFormat "%v:%p %R %m %>s %H conn=%X %D %O %I %k" metrics
counter apache_http_connections_total by server_port, handler, request_method, request_status, request_protocol, connection_status
counter apache_http_received_bytes_total by server_port, handler, request_method, request_status, request_protocol
counter apache_http_request_time_microseconds by le, server_port, handler, request_method, request_status, request_protocol
counter apache_http_request_time_microseconds_sum by server_port, handler, request_method, request_status, request_protocol
counter apache_http_request_time_microseconds_count by server_port, handler, request_method, request_status, request_protocol
counter apache_http_sent_bytes_total by server_port, handler, request_method, request_status, request_protocol

/^/ +
/(?P<server_port>\S+) / + # %v:%p - The canonical ServerName of the server serving the request. : The canonical port of the server serving the request.
/(?P<handler>\S+) / + # %R - The handler generating the response (if any).
/(?P<request_method>[A-Z]+) / + # %m - The request method.
/(?P<request_status>\d{3}) / + # %>s - Status.
/(?P<request_protocol>\S+) / + # %H - The request protocol.
/(?P<connection_status>conn=.) / + # %X - Connection status when response is completed
/(?P<time_us>\d+) / + # %D - The time taken to serve the request, in microseconds.
/(?P<sent_bytes>\d+) / + # %O - Bytes sent, including headers.
/(?P<received_bytes>\d+) / + # %I - Bytes received, including request and headers.
/(?P<keepalives>\d+)/ + # %k - Number of keepalive requests handled on this connection.
/$/ {
  ###
  # HTTP Requests with histogram buckets.
  #
  apache_http_request_time_microseconds_count[$server_port][$handler][$request_method][$request_status][$request_protocol]++
  apache_http_request_time_microseconds_sum[$server_port][$handler][$request_method][$request_status][$request_protocol] += $time_us

  # These statements "fall through", so the histogram is cumulative.  The
  # collecting system can compute the percentile bands by taking the ratio of
  # each bucket value over the final bucket.

  # 5ms bucket.
  $time_us <= 5000 {
    apache_http_request_time_microseconds["5000"][$server_port][$handler][$request_method][$request_status][$request_protocol]++
  }

  # 10ms bucket.
  $time_us <= 10000 {
    apache_http_request_time_microseconds["10000"][$server_port][$handler][$request_method][$request_status][$request_protocol]++
  }

  # 25ms bucket.
  $time_us <= 25000 {
    apache_http_request_time_microseconds["25000"][$server_port][$handler][$request_method][$request_status][$request_protocol]++
  }

  # 50ms bucket.
  $time_us <= 50000 {
    apache_http_request_time_microseconds["50000"][$server_port][$handler][$request_method][$request_status][$request_protocol]++
  }

  # 100ms bucket.
  $time_us <= 100000 {
    apache_http_request_time_microseconds["100000"][$server_port][$handler][$request_method][$request_status][$request_protocol]++
  }

  # 250ms bucket.
  $time_us <= 250000 {
    apache_http_request_time_microseconds["250000"][$server_port][$handler][$request_method][$request_status][$request_protocol]++
  }

  # 500ms bucket.
  $time_us <= 500000 {
    apache_http_request_time_microseconds["500000"][$server_port][$handler][$request_method][$request_status][$request_protocol]++
  }

  # 1s bucket.
  $time_us <= 1000000 {
    apache_http_request_time_microseconds["1000000"][$server_port][$handler][$request_method][$request_status][$request_protocol]++
  }

  # 2.5s bucket.
  $time_us <= 2500000 {
    apache_http_request_time_microseconds["2500000"][$server_port][$handler][$request_method][$request_status][$request_protocol]++
  }

  # 5s bucket.
  $time_us <= 5000000 {
    apache_http_request_time_microseconds["5000000"][$server_port][$handler][$request_method][$request_status][$request_protocol]++
  }

  # 10s bucket.
  $time_us <= 10000000 {
    apache_http_request_time_microseconds["10000000"][$server_port][$handler][$request_method][$request_status][$request_protocol]++
  }

  # "inf" bucket, also the total number of requests.
  apache_http_request_time_microseconds["inf"][$server_port][$handler][$request_method][$request_status][$request_protocol]++

  ###
  # Sent/Received bytes.
  apache_http_sent_bytes_total[$server_port][$handler][$request_method][$request_status][$request_protocol] += $sent_bytes
  apache_http_received_bytes_total[$server_port][$handler][$request_method][$request_status][$request_protocol] += $received_bytes

  ### Connection status when response is completed:
  # X = Connection aborted before the response completed.
  # + = Connection may be kept alive after the response is sent.
  # - = Connection will be closed after the response is sent.
  / conn=X / {
    apache_http_connections_total[$server_port][$handler][$request_method][$request_status][$request_protocol]["aborted"]++
  }
  # Will not include all closed connections. :-(
  / conn=- / {
    apache_http_connections_total[$server_port][$handler][$request_method][$request_status][$request_protocol]["closed"]++
  }
}
